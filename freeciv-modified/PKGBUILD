# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Modified by: elderica

pkgbase=freeciv
pkgname=freeciv
pkgver=3.1.5
pkgrel=1
pkgdesc="A multiuser clone of the famous Microprose game of Civilization (major clients with rule editor)"
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'cairo'
  'curl'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk3'
  'qt6-base'
  'hicolor-icon-theme'
  'icu'
  'libtool'
  'lua'
  'pango'
  'readline'
  'sdl2_mixer'
  'sdl2-compat'
  'sqlite'
  'xz'
  'zlib'
  'zstd'
)
makedepends=('python')
groups=('modified')
backup=('etc/freeciv/database.lua')
options=(strip docs staticlibs !lto)
source=("https://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}-${pkgver}.tar.xz")
sha512sums=('117c92300cffdf517d668243758d97f4457cda5511a8493c69530811d5e6e511ac40254069490b409466e9c35eb528dd3f1c10c46ff4730537445e21506c6b5c')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}" || exit 1
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-silent-rules \
    --enable-shared=yes \
    --enable-sys-lua=yes \
    --enable-client=gtk3.22,qt \
    --enable-fcmp=qt,cli \
    --enable-fcdb=sqlite3 \
    --enable-ruledit=yes \
    --enable-aimodules=yes \
    --with-missinglist
  make -j
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}" || exit
  make DESTDIR="${pkgdir}" install
}

