# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Modified by: elderica

pkgbase=freeciv
pkgname=freeciv
pkgver=3.1.4
pkgrel=4
pkgdesc="A multiuser clone of the famous Microprose game of Civilization (major clients with rule editor)"
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'cairo'
  'curl'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk3'
  'gtk4'
  'qt6-base'
  'hicolor-icon-theme'
  'icu'
  'libtool'
  'lua'
  'pango'
  'readline'
  'sdl2_mixer'
  'sdl2-compat'
  'sqlite'
  'xz'
  'zlib'
  'zstd'
)
makedepends=('python')
groups=('modified')
replaces=('freeciv-gtk4')
backup=('etc/freeciv/database.lua')
options=(strip docs staticlibs)
source=("https://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}-${pkgver}.tar.xz")
sha512sums=('71a4220c3d6cd3771d5f039a50932d0f80f9ca829821d778fe2bc16609110d8ab3b13b9483b3bb0e6860557f68e53d308865139224ae814dc5366e7fcf154a23')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}" || exit 1
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-silent-rules \
    --enable-shared=yes \
    --enable-sys-lua=yes \
    --enable-client=gtk3.22,gtk4,qt \
    --enable-fcmp=gtk3,gtk4,qt,cli \
    --enable-fcdb=sqlite3 \
    --enable-ruledit=yes \
    --enable-aimodules=yes \
    --with-missinglist
  make -j
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}" || exit
  make DESTDIR="${pkgdir}" install
}
